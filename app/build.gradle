apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    compileSdkVersion 29
    defaultConfig {
        applicationId "app.raybritton.magicmirror"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 4
        versionName "2.2"

        buildConfigField "String", "DARKSKY_KEY", "\"${properties.getProperty('darksky')}\""
        buildConfigField "String", "LATLNG", "\"${properties.getProperty('latlng')}\""
        buildConfigField "String", "WEATHER_DIFF_URL", "\"${properties.getProperty('weather_diff_url')}\""
        buildConfigField "String", "WEATHER_DIFF_KEY", "\"${properties.getProperty('weather_diff_key')}\""
        buildConfigField "double", "DISCHARGE_ALERT_THRESHOLD", "0.9"
        buildConfigField "double", "MIN_VALID_PRECIP", "0.1" //the lowest value that counts as rain
        buildConfigField "int", "MIN_VALID_WIND_SPEED", "10" //the lowest wind speed (kmph) before wind speed appears
    }
    signingConfigs {
        release {
            storeFile rootProject.file("release.jks")
            storePassword properties.password
            keyPassword properties.password
            keyAlias properties.alias
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def okhttp = "4.2.2"
    def retrofit = '2.6.2'
    def room_version = "2.2.1"
    def coroutines = "1.3.2"
    def lifecycle_version = "2.1.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation 'net.danlew:android.joda:2.10.3'

    implementation 'app.raybritton.elog:lib:0.4.3'
}
